# pomis:  На данный момент максимально возможное количество поинтов с Марией — 5, Минори — 6. Для выхода на рут надо хотя бы 4.
label ch4day1:
    scene bg city room day
    sl "— Эй, [playerName], давай, вставай! Генерал выступит с заявлением через 5 минут в квартире 39. Подъём!"
    "— Что? Какой генерал ещё?{w} А..."
    "Слава вытащил подушку из-под моей головы и потянул меня за руку."
    sl "— Подъём!"
    "— Да встаю я... Есть перекусить чего?"
    "Стоило мне наступить на пол голой ногой, как вся прохлада этого города пронзила моё тело. Как же тут отстойно."
    sl "— Потом покушаешь! Ты не понимаешь, что мы выполнили своё первое боевое задание?"
    menu:
        "— Мы базы лишились...":
            sl "— Зато мы получили бесценный опыт!"
            if minoriAlive==False:
                "— И не только базы..."
                # На экране полупрозрачно всплывает фейс минори
            else:
                "— Дороговата цена такого опыта..."
                sl "— Хватит ныть!{w} Ладно, я побежал!"
        "— Это надо отметить!":
            sl "— Точно! Ты прав!{w} Одевайся давай, я побежал..." 
            $ slavaPoints += 1
            $ wantToDrink = True;
            # pomis: Пока хз где делать проверку этих очков. Может в какой-то сюжетной развязке нам будет нужно хорошее отношение Славы
    "Побежал... Откуда в нём столько энергии?{w} Я неспешно собрался и поплёлся на лестничную клетку."
    if pickedManipulator:
        "По привычке засунул руки в карманы.{w} Ох, совсем забыл!"
        "Та штуковина, которой можно управлять людьми. Надо как-нибудь воспользоваться!"
        "Но не сейчас... Да и очень много жертв она производит. И семью мне это не вернёт..."
    "Дверь открыта.{w} В довольно большой комнате собралось человек 10. Из нашего отряда был только Слава."
    if yumingAlive:
        yu "— О, привет, солдат."
        "— Ага..."
    gen "— Итак, раз все собрались."
    "Все? По какому принципу нас тут собрали?"
    gen "— Как вы уже знаете, база разрушена. Выжившая часть персонала эвакуирована."
    gen "— Потери составили около половины состава. Большую часть погибших завалило."
    if mariePoints>3 and leadershipPoints>0:
        # Звук: стук сердца
        "— А как там Мария из лаборатории, генерал?"
        "Довольно дерзко было вот так прерывать речь генерала."
        if yumingAlive:
            "Судя по реакции Юминг, её этот вопрос тоже волнует."
        gen "— Научный персонал будет отправлен вместе с оборудованием на базу в Америке. Они дико извиняются за инцидент с кораблём и предоставили нам помещение в своей лучшей базе TESL."
        "— А с нами что будет? Почему нас не отправят туда же?"
        gen "— Вас тоже распределят по базам, скорее всего. Никто не готов принять сразу столько людей. Так что, подождёте немного в этом городе."
    else:
        gen "— По поводу продолжения военной службы. Вам придётся немного подождать в этом городе, пока какая-нибудь база не сможет вас всех принять. Научный персонал вместе с оборудованием отправляется в Америку."
        gen "Американцы дико извиняются за инцидент с кораблём и предоставляют нам помещение в своей лучшей базе TESL под лабораторию." 
        sl "— Почему мы отдаём учёных америкосам?"
        gen "— Ты забыл, что мы внеполитическая структура? Что в Америке, что в России, у нас одна цель, один враг."
    gen "— Вопросы?"
    if yumingAlive:
        yu "— Когда отправляются учёные?"
        gen "— Сегодня вечером."
    elif mariePoints>3 and leadershipPoints>0:
        "— Когда отправляются учёные?"
        gen "— Сегодня вечером."
    sl "— Как нашу базу вообще нашли?"
    gen "— Судя по всему, новобранец Павел был шпионом."
    if inferno==False:
        "— Он же человек... Зачем ему быть на стороне наших врагов? Какой в этом вообще смысл?"
        yu "— Может, пообещали чего-то?"
        gen "— Думаю, он просто озлоблен на общество. Вот и ищет способы навредить."
        "Как-то не очень убедительно..."
    else:
        "Хм... Шпион Инферно? Было бы интересно выйти с ним на связь."
    gen "— Раз вопросов больше нет... Все свободны."
    "Люди стали поспешно вставать со своих мест и уходить. Я только вышел из квартиры в гордом одиночестве, как меня догнал Слава."
    # Сцена подъезд
    scene bg city corridor with dissolve
    show slava suspicious:
        xpos 0.57 ypos 1.02 xanchor 0.5 yanchor 1.0 
    sl "— Эй, [playerName], изучим город?"
    menu:
        "— Хорошая идея.":
            $ walkedWithSlava = True
            $ slavaPoints += 1
            sl "— Погнали!"
            hide slava with dissolve
            scene bg city street day with dissolve
            "На улице немного прохладно, но солнечно."
            "Мы обошли пару кварталов, но не нашли вообще ничего интересного. Если в мире есть воплощение унылости, то это [cityName]."
            "Серые дома везде. Одинаковые люди, одинаковые улицы. Судя по всему, единственная достопримечательность здесь это памятник Ленину в центре города."
            # Сцена город. Солнечно.
            "До чего же тут убого. По дороге почти не говорили со Славой."
    
            if mariePoints>0 and mariePoints<=3:
                "Серость этого дня была разбавлена встречей с Марией."
                ma "— О, привет, [playerName]!"
                "— Привет."
                ma "— Я вечером уезжаю из этой дыры, хех!"
                sl "— Будешь теперь за пиндосов?"
                ma "— Да ну тебя."
                "— Тебе там лучшую лабораторию подготовят, да?"
                ma "— Ещё бы!"
                "— А нас оставят в этом Мухосранске до тех пор, пока какая-нибудь база не соблаговолит нас принять. Отстой."
                ma "— Это временно же."
                if yumingAlive:
                    ma "Кстати, ты так до сих пор не обзавёлся телефоном?{w} Ну ладно, как приеду в Штаты, сразу же сообщу Юминг."
                    ma "— Если интересна моя судьба, спросишь у неё."
                    ma "— Я пойду, прогуляюсь перед путешествием... Что в этом городе есть вообще?"
                    "— Площадь Ленина. И десятки одинаковых улочек."
                    ma "— Как тоскливо! Будто бы путешествие на 50 лет назад."
                "— Удачной дороги, что ли."
                ma "— Спасибо!"
                "Наверное, мне будет её не хватать. Надо бы привыкнуть, что люди уходят из моей жизни с завидной скоростью."
            if mariePoints>3:
                "По пути в сторону нашего временного дома встретили Марию."
                ma "— О, вот ты где! Везде тебя ищу..."
                "— Привет."
                sl "— Зачем ищешь? М-м?"
                "Слава сделал хитрое лицо, но был проигнорирован."
                ma "— Я тут уезжаю вечером в Штаты..."
                "— Да, слышал уже. Тебе там лучшую лабораторию подготовят, вроде бы. Разве не рада?"
                ma "— Ну{w}, да, наверное. Не знаю."
                sl "— Будешь теперь за пиндосов?"
                "Слава снова был проигнорирован."
                "— Хм-м. А ты можешь меня с собой взять? Не хочу тут оставаться."
                "На самом деле, не хочу расставаться с Марией. За те несколько дней, что мы знакомы..."
                sl "— Сталина на вас нет..."
                # bulka: Эпик. Просто в голос заржал на этом моменте.
                ma "— Думаешь, можно? Ты ведь знаешь английский?"
                "— Прочитать что-то несложное могу."
                ma "— Ничего, научишься. Попробую тебя забрать как-нибудь."
                ma "— Ладно, я побежала!"
            if knowIvanName:
                "У нашего подъезда стоял Иван, докуривающий сигарету."
                pi "— О, здарова, мужики! Я теперь ваш координатор!"
                sl "— Да, мы в курсах! Круто!"
                "Такой радостный. Кажется, он забыл, что стало с предыдущим."
            else:
                "У нашего подъезда стоял пилот, докуривающий сигарету."
                pi0 "— О, здарова, мужики! Я теперь ваш координатор!"
                "— А зовут тебя как?"
                pi "— До сих пор не знаешь? Иван я."
            "— Когда занятия?"
            pi "— Ха! Через 15 ми..."
            "Наш новый координатор немного завис."
            pi "— Вы где живёте?"
            sl "— 26 квартира."
            pi "— Вот, значит, там и встретимся. Дверь не закрывайте."
            "— А чего сразу у нас?"
            "Иван бросил окурок и скрылся в подъезде, оставив мой вопрос без ответа."
            "Мы вернулись в служебную квартиру. Постепенно все собрались."
            jump endOfDay41
        "— Нет, как-то не хочется.":
            if mariePoints>3:
                "Какой ещё город. Марию бы найти."
                "Где она вообще может быть?"
            sl "— Ну и ладно, сам прогуляюсь."
            hide slava with dissolve
            "Слава ушёл вниз по лестнице."
            if mariePoints>0 and mariePoints<=3:
                
                show marie corridor smiling onlayer master:
                    xpos 0.19 ypos 1.0 yanchor 1.0 zoom 0.97 
                ma "— О, привет, [playerName]!"
                "— Привет."
                ma "— Я вечером уезжаю из этой дыры, хех!"
                "— Да, слышал уже. Тебе там лучшую лабораторию подготовят, да?"
                ma "— Ещё бы."
                "— А нас оставят в этом Мухосранске до тех пор, пока какая-нибудь база не соблаговолит нас принять. Отстой."
                ma "— Это временно же."
                if yumingAlive:
                    ma "Кстати, ты так до сих пор не обзавёлся телефоном?{w} Ну ладно, как приеду в Штаты, сразу же сообщу Юминг."
                    ma "— Если интересна моя судьба, спросишь у неё."
                    ma "— Я пойду, прогуляюсь перед путешествием..."
                "— Удачной дороги, что ли."
                ma "— Спасибо!"
                hide marie
            elif mariePoints>3:
                show marie corridor smiling onlayer master:
                    xpos 0.19 ypos 1.0 yanchor 1.0 zoom 0.97 
                ma "— О, вот ты где! Везде тебя ищу..."
                "— Привет."
                ma "— Я вечером уезжаю, приходится покидать родную страну..."
                "— Да, слышал уже. Тебе там лучшую лабораторию подготовят, вроде бы. Разве не рада?"
                ma "— Ну{w}, да, наверное. Не знаю."
                "— А нас оставят в этом Мухосранске до тех пор, пока какая-нибудь база не соблаговолит нас принять. Отстой."
                "— Хм-м. А ты можешь меня с собой взять? Не хочу тут оставаться."
                "На самом деле, не хочу расставаться с Марией. За те несколько дней, что мы знакомы..."
                "Совсем раскис."
                ma "— Думаешь, можно? Ты ведь знаешь английский?"
                "— Прочитать что-то несложное могу."
                ma "— Плохо-плохо. Ничего, научишься. Попробую тебя забрать как-нибудь."
                "— Как-нибудь? Ты же сегодня уезжаешь."
                ma "— Не знаю."
                ma "— Ладно, пошли, может, пройдёмся?"
                "— Да, пошли."
                "Ой. А Славе отказал. Будет тупо, если встретим его.{w} Хотя, какая разница."
                # сцена город
                hide marie
                scene bg city street day with dissolve
                "Какой скучный город. Одинаковые серые улочки, дома, люди. Такое чувство, что это воплощение унылости."
                ma "— Эй, чего задумался?"
                "— Как здесь вообще люди живут?"
                ma "— Думаю, что не так уж и плохо. А ты откуда?"
                "— С Академки.{w} Ой, то есть, Москва. А ты откуда?"
                # pomis: дс же
                ma "— Вообще, родилась в Варшаве, но из-за конфликтов в стране нам пришлось уехать в Россию. Так что, выросла уже в Калининграде."
                "— А как ты попала на военную базу в другом конце мира?"
                ma "— Ну я на оборонку работала. В отделе разработки. И в один день, мне предложили занятие поинтересней, на что я с радостью согласилась."
                "— Хорошо, когда работа совпадает с увлечением, наверное."
                ma "— Не представляю, как может быть иначе."
                ma "— А ты кем работал?"
                "— Да никем... Пытался выучиться на программиста, но, не осилил."
                # pomis: плохо помню прошлое гг, исправьте, если не так
                ma "— Что там можно не осилить?"
                "Я легонько толкнул Марию."
                "— Так, давай без интеллектуального давления!"
                ma "— Что такого я сказала?"
                "— Не все такие гении, как ты."
                ma "— Ладно, я просто привыкла издеваться над работниками моего отдела. Мне можно."
                if knowIvanName:
                    "Возвращаясь в наш подъезд, встретили Ивана."
                    pi "— Какая миленькая парочка! Гы."
                else:
                    "Возвращаясь в наш подъезд, встретили пилота."
                    pi0 "— Какая миленькая парочка! Гы."
                ma "— Ваня, да мы просто прогулялись..."
                if knowIvanName == False:
                    "Значит, Иван."
                    $ knowIvanName = True
                pi "— Ага."
                "Иван едва заметно подмигнул мне."
                pi "— Ах да, [playerName], я твой новый координатор. Через 10 минут собираемся."
                "— Да, я в курсе. А где?"
                pi "— О... Даже не думал об этом. Давай у тебя! Какая квартира?"
                "— 26. А что, почему не у тебя?"
                pi "— Лучше тебе этого не знать! Бу-га-га." 
                "Иван бросил окурок и растворился в подъезде."

            # Вернулся домой 
            else:
                scene bg city room day with dissolve
                "Я вернулся в свою служебную квартиру. Совсем ничего не хочется делать. А тут всё так же прохладно."
                "Можно поспать. Или посмотреть телевизор, а потом уснуть."
                # Звук стука в дверь
                "Знакомый голос" "— Есть кто?"
                "Ну кого ко мне занесло? Только отдохнуть собирался..."
                "— А кто спрашивает?"
                "Знакомый голос" "— А кто отвечает?"
                "— Тот, к кому вы стучитесь."
                "Знакомый голос" "— А к тому ли я стучусь?"
                "Уже раздражает."
                "— Сейчас узнаешь."
                "Открыл дверь незваному гостю."
                pi "— Гы-ы-ы. Здарова! Я ваш новый координатор! Иван Крылов!"
                if knowIvanName == False:
                    $ knowIvanName = True
                    "Так вот как его зовут."
                else:
                    "— Да, я в курсе..."
                pi "— Через час будет занятие. Расскажу вам то, что должен знать каждый воин!"
                "— И где?"
                pi "— О... Об этом я не подумал. Но точно не у меня."
                pi "— Давай прямо тут!{w} Ладно, бывай!"
                "И убежал. Что за координаторов нам подсовывают?"
                "Значит, через час. И почему у него нельзя?"
                "Ну и ладно. Посмотрю телевизор, что ли."
                "...Завтра ожидается похолодание, будут обильные дожди. Ветер..."
                "Скучно.{w} Перекусить бы. Никто ведь не организует нам питание? Всё самому готовить?"
                "Я стал рыться по квартире в поисках хоть чего-то съедобного."
                "Ничего. Да даже если бы что-то и было...{w} Это Тоня была профи в этом деле.{w} А я...{w} Не приходилось как-то."
                call flashback4
                "Почему мне вспомнился именно этот момент? Отвратительно."
                if minoriAlive and minoriPoints>2 and minoriPoints>iscraPoints:
                # bulka: В случае с рутом инферно минорипойнты не набираются же? Не помню точно.
                    "Снова стук в дверь. Ну кого в этот раз ко мне занесло?"
                    mi "— Это я!"
                    "Вот её лёгкий акцент я сразу узнаю."
                    # Звук открывающейся двери
                    "— Какими судьбами?"
                    mi "— Ну... Новый координатор сказал, что занятия будут тут."
                    "— Через час."
                    mi "— Да. Ну я не знала, чем себя занять. А ты тут живёшь?"
                    "— Живёшь — громко сказано. Ну, да. Ты заходи, что ли."
                    # Сцена гостиная
                    "— Как ты себя чувствуешь?"
                    mi "— Я? Даже не знаю. Не могу разобраться."
                    "Что? Как это?"
                    "— Ну хоть в чём-то ты сейчас уверена?"
                    if withMinoriOnZumwalt:
                        # Сюда можно попасть только в солдатском руте
                        mi "— Да."
                        "— В чём же?"
                        mi "— В том, что я пропаду без тебя."
                        "Ничего себе заявление. Да и не ожидал, что она сможет вот так прямо это сказать."
                        "И как ей ответить-то?"
                    else:
                        mi "— Я никому не могу тут довериться. Это общество вокруг.{w} Дома было гораздо проще."
                    mi "— Я боюсь их всех. Я не могу доверять таким странным людям."
                    "— Чем же они странные?"
                    mi "— Не знаю. Я чувствую это. Один только Тенко. От одного его взгляда мне хочется испариться."
                    # bulka: Этих пойнтов максимум 2 пока что. И это опять же не настроенность против тенко, а способность ему противостоять. (аля пойнты лжи и тд)
                    # pomis: ok
                    if withMinoriOnZumwalt==False:
                        "— Ну мне-то ты хоть доверяешь?"
                        mi "— Пожалуй, больше, чем остальным."
                    "— А Слава? Даже он тебя пугает?"
                    mi "— Он с таким презрением относится ко мне. Как к бесполезному мусору."
                    mi "— Хоть и не говорит это открыто."
                    mi "— Боец из меня никакой. Но я никогда и не хотела им становится. Я тут случайно!"
                    menu:
                        "— Все мы тут случайно. Тебе придётся приспособиться.":
                            mi "— Я никогда не смогу кого-то застрелить. Даже если это какой-то монстр, убивший сотни людей."
                        "— Ты всегда можешь положиться на меня!":
                            $ minoriPoints += 1
                            mi "— Я знаю это."
                            "Минори хитро улыбнулась."
                    "— Слушай. А ты случайно готовить не умеешь?"
                    mi "— Конечно умею, а ты что, нет?"
                    "— Ну так. Были бы продукты, что-нибудь бы сделал..."
                    mi "— Жди здесь!"
                    "Спустя 10 минут Минори принесла небольшой пакет продуктов."
                    mi "— Я утром в первую очередь закупилась!"
                    "— Ну а у меня было собрание. Ганц объяснял ситуацию."
                    mi "— Опасный он человек. Мы многого о нём не знаем."
                    "— Что к примеру?"
                    mi "— Сколькими жизнями он пожертвовал?{w} Даже не хочу об этом думать, и так ночью спать не могу. Тебе что приготовить?"
                    "— На твой вкус."
                    mi "— Хах. Что бы ты ни сказал, из риса и рыбы много блюд не приготовить."
                    # bulka: Японцам это расска... Ой, она же японка...
                    "Постепенно мы перешли на пустую болтовню. Минори хоть и слабая, но умеет чувствовать людей. И готовит неплохо."
                    # Звук открывающейся двери.
                    sl "— У нас тут сейчас занятия будут!"
                    "— Мы в курсе."
                    "Хм. Я бы раньше не заметил этого презрительного взгляда в сторону Минори. Ничего себе. Надо бы их как-то сдружить."

                # Минори не постучала в дверь (Мисаки не пришла)
                else:
                    if inferno and pickedManipulator:
                        "Всё таки, хочется испытать мой трофейный манипулятор.{w} Не сейчас."
                    else:
                        # Звук открывающейся двери.
                        sl "— У нас тут сейчас занятия будут!"
                        "— Я в курсе."

                jump endOfDay41
    jump endOfDay41

label endOfDay41:
    "Постепенно все собрались."
    pi "— Итак! Раз все в сборе, начнём.{w} Мы просрали всё своё самое крутое учебное оборудование, так что буду объяснять на пальцах."
    pi "— Как вы уже поняли, Инферно обладает технологией управления сознанием."
    if pickedManipulator==False:
        pi "— Устройство было найдено и передано в исследовательский отдел."
        pi "— Учёные успели немного поковыряться в нём. Судя по составу, оно изготовлено из пород, встречающихся в определённых местах Тихоокеанского шельфа. Туда будет направлена исследовательская платформа для установления точных координат. "
        sl "— Найдём и поджарим их?"
        pi "— Ха! Именно так!"
        if inferno:
            "Ну и фашисты."
        else:
            "— Они это заслужили!"
        ten "— Я так понимаю, города расположены на солидной глубине? Как вы собираетесь их атаковать? Бурить дно океана?"
        sl "— Ядерным ударом по дну!"
        # pomis: Тут я стал делать что-то похожее на Тихоокеанский рубеж...
        "— Не силён в физике, но как бы цунами тут не спровоцировать."
        pi "— Вообще, я пока не знаю, как планируется нападать. Но если у нас будут координаты, лучшие умы придумают что-нибудь."
        pi "— По поводу контроля разума — наши головастики выяснили, что устройство излучает особый спектр волн, воздействующий на что-то там в мозгу из-за чего что-то там происходит и вы начинаете подчиняться командам владельца устройства."
        "Божественное объяснение..."
        isc "— Что-то вроде биологической обратной связи?"
        pi "— Спроси что попроще."
        "Какой же он тупой."
        pi "— Ваши подлодки уже оснащены защитой от излучения. Сейчас идёт активная разработка компактных устройств для противодействия этим волнам на случай повторения боёв как на эсминце."
        # bulka: Тут будет условие, спрашивающее о киллах тенко на эсминце, если да, то гг говорит "Это больше было похоже на бойню, нежели на сражение."
        # pomis: ok
    else:
        pi "— Мы пока не знаем, как им это удаётся. На корабле не было обнаружено никаких необычных устройств или веществ."
        "Не там искали, хех."
        pi "— Но мы обязательно придумаем как с этим бороться. Скорее всего, эта технология основана на волновом воздействии на мозг.{w} В ближайшее время, наши яйцеголовые соорудят что-нибудь против этой фигни. Подлодки же изначально экранированы от подобного вида излучений."
        # bulka: Из меня куёвый физик, но сто пудов есть какое-нибудь излучение, что влияет на мозговую активность. Зафигачьте её тут/её описание.
        # pomis: Оно есть, но тут некому об этом рассказать.
        # bulka: Хз, попытался сделать продолжение, но я хз узнают ли о этом городе методом анализа курса эсминца или нет.
        "Как они собираются бороться против неизвестного оружия?"
    "Следующие минут сорок Иван рассказывал о своих боевых вылетах. Как же он любит хвастаться. Прямо как Жорик."
    pi "— Кстати, должен заметить, вы неплохо сработались как команда. Для укрепления боевого духа предлагаю отметить вашу первую победу."
    sl "— Хорошая идея!"
    if wantToDrink:
        "— Я тоже не против..."
    else:
        "Остальные одобрительно зашумели."
        "— Отмечать? Было бы что."
        sl "— Опять ты за своё..."
    if wantToDrink:
        "Мы пошли за Иваном и поспешно покинули квартиру." 
    else:
        "Все пошли за Иваном и поспешно покинули квартиру."
    if inferno==False and mariePoints>3:
        ma "— Ну наконец-то вы закончили."
        "Я вздрогул от неожиданности."
        ma "— Через час уплываем. За тебя договорилась. Тебе есть что забрать с собой?"
        "— О, круто! Нет, я налегке."
        if marieSaidAboutBeer:
            "Час. Успею до магазина сходить, она же жаловалась, что на базе негде взять пиво. Всё-таки увидеть пьяную Марию будет незабываемым зрелищем."
            if lazynessPoints>1:
                "Я даже преодолею свою лень ради такого случая."
        ma "— Ну раз уж ты налегке, то пошли сразу на корабль!"
        "— Корабль? А чего не самолёт? У меня за последнее время доверие к кораблям немного упало."
        ma "— Кое-какие экспериментальные образцы и оборудование удалось вытащить из завалов. Такие громадины только кораблями перетаскивать можно. Ну а нас — заодно."
        if stolenFlash:
            "Хех, она не знает, что из лаборатории спасся ещё один образец — флешка с неизвестным содержимым."
        ma "— Я тоже налегке, практически всё осталось под завалами."
        "— Ладно, погнали."
        "Мария вызвала такси и мы прибыли к порту. Здоровенный современный корабль явно не вписывался в общую картину разрухи и отсталости."
        "Рядом стайками толпились люди в белых халатах, несколько людей в военной форме. Ни одного знакомого лица. Думаю, я тут единственный «способный»."
        "Ведь на базе были и сотни других людей. Интересно, повара выжили?"
        "— Кстати, а ты ничего не знаешь про секретную столовую?"
        ma "— Сейчас уже это не имеет никакого значения."
        "Как-то очень холодно ответила."
        jump ch5marie
    if wantToDrink:
        jump ch4day1end 
    else:
        "Мне ничего не оставалось, как лечь спать."
        if inferno:
            # Сцена — вид с кровати, свет выключен
            "И как мне поступать дальше? Я же понятия не имею! Как мне выйти на связь с Инферно?"
            if killedPeopleCount>1:
                "Я убивал людей ради своей цели, но вот только моя цель сейчас даже мне самому не ясна..."
            elif killedPeopleCount==1:
                "Я убил человека ради своей цели, но вот только моя цель сейчас даже мне самому не ясна..."
            elif killedPeopleCount==0:
                # pomis: пока что сюда нельзя попасть, на всякий случай прописал
                # bulka: Я тоже не знаю. Может потому, что тут ничего и не надо? В рут инферно попадают через убийство же. Офк, возможно ещё один переход зафигачим, но лишь тогда и можно будет тут что-то сделать.
                "Сейчас даже мне самому не ясна моя цель. Зачем я всё это делаю?"
            "Отомстить за семью? А что потом?"
            menu:
                "Стать серийным убийцей?" if agressionPoints>3:
                    if agressionPoints>4:
                        # bulka: Немножко не оч имхо. За очки очки давать.
                        # pomis: Ну да, верно.
                        "Хах. Хорошая идея."
                        "Но не лучшая."
                    else:
                        "Му-ха-ха!{w} Нет."
                "Отдохнуть от всего?":
                    $ lazynessPoints += 1
                    "Наверняка, у меня будут средства обеспечить себе безбедную жизнь..."
                    "Скучновато, конечно."
                "Самоубийство?":
                    "Что-то мне подсказывает, что этим всё кончится."
                    "Я не смогу жить в обществе, которое ненавижу."
                    "Может, я смогу ужиться в обществе Инферно?"
                    "Интересно, как они вообще живут? К чему стремятся?"
                    "Я не смогу убить себя, пока в жизни есть какой-то интерес.{w} Но как только я его потеряю..."
            "Чёртовы мысли. Спать хочу!"
            "А что, если Инферно установили за мной слежку?"
            "Я как ошарашенный спрыгнул с кровати."
            "Вдруг они прямо сейчас следят? И читают мои мысли?"
            # bulka: Я что-то пропустил и его Искра покусала? Чё-то слишком глупо выходит.
            # pomis: Смягчил. Но всё же, хочу, чтобы у гг были такие мысли.
            "Боже, что за бред...{w} Занавески колыхались от сильного ветра. Эти доисторические деревянные окна настолько прогнили, что даже открывать не нужно, чтобы проветрить."
            # смотрит в окно
            "Какая ужасная погода... Зачем они пошли гулять?"
            "Жалко."
            # сцена — вид с кровати
            # закрываются глаза


            jump ch5enlil
        if minoriPoints>=3 and minoriAlive and inferno==False:
            # bulka: А почему у нас ограничение по инферно постоянно? Самое прикольное, что иногда только в инферно можно очки и набирать...
            # pomis: Что-то не смог найти, где именно при инферно можно получить поинты минори. 
            # pomis: Возможность замутить с Минори при инферно сценарии бы добавила ещё одно разветвление, при чём, довольно сложное — Минори хочет быть в безопасности, ей незачем следовать за гг во всякие авантюры. Да и я вроде бы дал ей чутьё, которым она может понять истинные намерения человека. Местами это обыгрывалось. Так что, её будет отталкивать от гг, если она будет чувствовать что-то неладное.
            # pomis: ослабил условие на один поинт. Ибо ни один тестер не смог с ней замутить.
            mi "— Эй, [playerName], давай прогуляемся. Не хочу я с ними ничего отмечать. Не вписываюсь в их компанию."
            "— Я только спать собрался."
            "Хотя."
            "— Ладно, пошли. А куда?"
            mi "— Куда глаза глядят. Пошли."
            "Я накинул на себя какую-то толстовку. Наверное, Славина. Подумать только! У меня даже своей нормальной одежды нет!"
            "На небе сгущались тучи, а светило приближалось к горизонту."
            "— Куда пойдём?"
            if walkedWithSlava:
                "— Мы тут немного изучили окрестности со Славой, ничего интересного."
            jump ch4walkWithMinori
        elif iscraPoints>1:
            isc "— А ты чего? Жду тебя через 5 минут внизу."
            "— Я уже думал, что вы все ушли."
            isc "— Как видишь, нет. Тебя все ждут. Давай, быстрее."
            "— Я бы лучше поспал..."
            isc "— В гробу отоспишься. А если будешь бросать товарищей, то скоро там и окажешься."
            "— А ты вошла в роль, да?"
            isc "— На самом деле нет, меня просто попросили тебя вытащить."
            "Видимо, выбора у меня нет, накинул первую попавшуюся кофту, а через несколько минут я уже стоял на улице с товарищами."
            jump ch4day1end
        # pomis: а тут настолько сычевальный вариант, что гг со всеми девушками в холодных отношениях. Можно дать ему шанс замутить с кем-то дальше. 
        jump ch5day0

label ch4day1end:
    "Мы шли по какой-то безлюдной улице."
    "Кажется, этот город создан для того, чтобы спаивать людей. Я сам очень редко пью, но в такой ситуации я даже не знаю, что мне ещё делать."
    sl "— Погнали в бар! Отметим как мужики!"
    if minoriAlive:
        mi "— Не-е..."
        "Слава посмотрел на Минори так, что она не стала продолжать."
        ten "— Я тоже не любитель алкоголя."
    else:
        ten "— Ну, я не любитель алкоголя."
    if antiTenkoPoints>2:
        "— Ещё бы. Тебе нужно быть трезвым как стёклышко, чтобы уследить за каждой мелочью?"
        "Тенко сделал вид, что не услышал меня."
    pi "— И что тогда, не будем пить?"
    if minoriAlive:
        mi "— Можно просто прогуляться."
        if minoriPoints>=3:
            "Вот да, я бы лучше прогулялся с ней, чем бухать."
        sl "— Да тут негде гулять. Этот город — воплощение серости."
        isc "— Ты тоже."
        "Кажется, уже все стали игнорировать колкости Искры."
        # bulka: В реальности ей бы уже давно тёмную устроили и она бы больше не возникала
        # bulka: Хотя Дадин возникал. Хотя может он всё выдумал.
    else:
        ten "— Можно и по-другому отметить."
        pi "— Отстой какой-то."
    sl "— Вань, пошли в бар уже!"
    pi "— Думаешь, в этом Зажопинске есть нормальный бар?"
    sl "— А что здесь вообще есть?"
    pi "— Ничего. А у меня в сумке есть всё, что нам нужно. Главное, найти укромное место."
    "Иван остановился и кивнул в сторону дворов."
    sl "— А может, лучше в квартире?"
    pi "— Фу, ты чего? Свежий воздух, все дела... Да и я бы не рискнул попадаться Ганцу в нетрезвом виде. Он мне больше не доверит ни один вертолёт."
    isc "— Я бы вообще тебе ничего не доверила."
    # bulka: Вангую ббпе в одной из глав, когда она истерить в критической ситуации начнёт
    # pomis: Как же без этого
    pi "— Да тише ты."
    pi "— А ты что думаешь, [playerName]?"
    menu: 
        "— Доставай уже!":
            pi "— Во, нормальный настрой!"
            jump ch4drink
        "— Бар есть в любом городе.":
            $ slavaPoints += 1
            # bulka: "Каждый выбор на что-то влияет"...
            # pomis: Верно. Я пока не придумал как.
            # pomis: Ранее Слава говорил о баре. Пусть будет +1
            "— Особенно в таком. Как здесь люди вообще развлекаются?"
            pi "— Ты в этом уверен? Находил?"
            "— Не уверен."
            sl "— Ладно, давай уже тут разберёмся с содержимым твоей сумки."
            pi "— Как скажешь!"
            jump ch4drink

        "— Я за прогулку." if minoriAlive:
            if walkedWithSlava:
                sl "— Мы же чуть со скуки не умерли, когда гуляли тут."
                "— Может, ночью здесь интереснее?"
            if inferno==False and minoriPoints>=3:
                isc "— Да иди уже гуляй со своей японкой, думаешь мы не замечаем, как вы смотрите друг на друга?"
                "Минори залилась краской."
                pi "— Ну не так прямолинейно же!"
                "Наши товарищи пошли во двор, а мы с Минори остались на улице."
                jump ch4walkWithMinori
            else:
                mi "— Ну ладно, не буду против большинства..."
            jump ch4drink


label ch4walkWithMinori:
    mi "— А давай покинем город?"
    "— И никогда не вернёмся?"
    mi "— Да нет, я бы хотела посмотреть на природу этого места."
    "— Мне не нравятся эти тучи над городом. Если ночью пойдёт дождь и нам будет негде укрыться, то мы..."
    mi "— Ладно, я поняла. И как ты защитишь меня, если боишься даже выйти из города?"
    "— Я могу защитить тебя от безрассудных поступков, например."
    mi "— То есть, мой главный враг — это я?"
    "— Ну точно не я."
    # pomis: угарел с своего собственного текста
    mi "— Ладно, пошли хотя бы парк найдём."
    "— Как скажешь."
    "..."
    play music telescope
    mi "— Парк имени Горького? Почему именно его именем называют все парки в этой стране?"
    "— Я даже не помню, кто это такой."
    mi "— Писатель же. Я даже читала его, правда не понравилось.{w} И вообще, это не важно."
    "Минори смотрела на меня ожидающим взгядом. Сложная.{w} Ладно."
    "Я взял её за руку и повёл в парк. Мы прошли через старые проржавевшие ворота. Удивительно, что ничего не отвалилось, когда я открывал их."
    mi "— Как думаешь, в этом парке кто-то есть сейчас? Он выглядит заброшенным."
    "— Парк небольшой, так что, если кто-то и есть, то мы обязательно пересечёмся."
    mi "— Откуда ты знаешь, что он небольшой?"
    "— А кто будет строить нормальный парк в таком убогом городе?"
    mi "— Да уж."
    mi "— Расскажи что-нибудь о своём прошлом?"
    "— А что тебя интересует?"
    mi "— Был с девушкой?"
    "— В каком плане?"
    "Она выглядит такой смелой только со мной наедине. Меня это немного пугает. И эти прямолинейные вопросы сбивают меня с толку."
    mi "— В обычном!{w} Ну и ладно. Я поняла. Я тоже никогда не была сильно близка с противоположным полом."
    mi "— Хотя, многие ребята постоянно приглашали меня куда-то... Мне они были абсолютно безразличны."
    "— Вот прямо все? Такое бывает?"
    mi "— Ну как так? Найти любовь в своём же городе? Да ещё и в своей школе? Когда в мире миллиарды людей. Вдруг, человек, который создан для меня, сейчас находится в тысяче километров от меня в какой-нибудь большой северной стране?"
    "Мы остановились около небольшого ручейка, бегущего мимо дорожки. Солнце уже почти исчезло из вида, а всё небо заслонили тучи."
    "Я встал перед Минори и взял её за вторую руку. Смотрел в её глубокие голубые глаза."
    "Моё сердце билось так, что казалось, что Минори слышит его стук."
    "Мы закрыли глаза."
    "В этот момент ничто не имело для меня значения, кроме девушки, вкус губ которой я ощущал."
    "Её теплое дыхание обжигало мои щеки, а каждое прикосновение вызывало дрожь и заставляло мой пульс учащаться."
    "Я не знаю, сколько времени мы так провели. Может, всего минуту, может, три. Или десять. Я не ощущал времени."
    mi "— Ну теперь-то ты обязан защитить меня от этого мира."
    "Снова этот хитрый взгляд. По-доброму хитрый, не как у Тенко.{w} И почему в такие моменты я вспоминаю таких неприятных людей?"
    "— А кто меня защитит?"
    mi "— От меня? Никто." 
    "Она слегка оттолкнула меня и засмеялась."
    mi "— Ладно, пошли обратно, а то прохладно."
    "Обратный путь мы провели почти в полном молчании. Я погрузился в свои мысли, вспоминал всех тех девушек, которые мне когда-то нравились, но почему-то все они отвергали меня, а те немногие, которые обращали на меня внимание вызывали у меня отвращение."
    "Минори стала напевать какую-то незнакомую мне мелодию, когда мы приближались к дому."
    "Вернуть бы Тоню."
    mi "— Спокойной ночи!"
    "— И тебе..."
    # "В этот момент меня обрызгало кровью из лопнувшей словно арбуз головы Минори."
    # "Спустя мгновенье прогремел выстрел.{w} Я стоял в шоке неспособный двинуться с места.{w} В голове роились тысячи мыслей, но я не мог осознать ни одну из них."
    # bulka: Извиняюсь. Не выдержалXD
    # pomis: Я её драматичнее убью как-нибудь
    jump ch5day0


label ch4drink:
    scene bg city street night with dissolve
    play music telescope
    $ celebratedVictory = True
    "Мы вошли в дворик и расположились у скамеек."
    "Иван достал из сумки две бутылки вина, открыл их штопором, одну вручил мне, другую оставил себе."
    # bulka: Вина? Не водки? А как же "VODKA VODKA BALALIKA PUH PUH PUH PUH!"?
    sl "— Уж без стаканчиков, извиняйте."
    isc "— Чего так мало?"
    # bulka: Хочу больше теорий заговора от пьяной искры!
    # pomis: Я попробую
    "Я сделал небольшой глоток. Никогда не был особым любителем алкоголя. "
    $ underVine = True
    sl "— А ну дай сюда!"
    "Слава осушил половину бутылки. Опасный тип."
    sl "— Закусь!"
    pi "— Эх, а вот закусить нечем."
    show iscra sad  onlayer master:
        subpixel True xpos 0.19 ypos 1.06 xanchor 0.5 yanchor 1.0 rotate None alpha 1.8 additive 0.0
    isc "— Штопор взял, а закуску нет? Ни за что не поверю, что ты случайно забыл."
    isc "— Где мои фруктики, мужчина?"
    "Я вот сейчас понять не могу, она самоиронизирует над своей постоянной подозрительностью? Или серьёзно?"
    pi "— В магазине. Так пей."
    if yumingAlive and yumingPoints<3:
        "Слава с Юминг о чём-то разговаривали и немного обособились от нас. Мы же довольно быстро справились с бутылками."
    elif minoriAlive:
        "Тенко с Минори о чём-то разговаривали и немного обособились от нас. Мы же довольно быстро справились с бутылками."
    else:
        "Мы довольно быстро справились с бутылками."
    isc "— Правда или действие!"
    "Этого ещё не хватало!"
    pi "— Хорошая идея. С тебя и начнём!"
    isc "— Правда. Я люблю правду."
    pi "— Кто самый крутой из новобранцев на твой взгляд?"
    "Ваня хитро улыбнулся."
    isc "— Думаю, Павел. Смотался под шумок. Красавчик!"
    pi "— Этого засранца мы ещё поймаем."
    # pomis: он не должен говорить "Хах". Пусть это говорит кто-то другой. Лучше всего подойдёт Марии.
    isc "[playerName]. Правда или действие?"
    menu:
        "Правда":
            isc "— Сколько людей ты убил?"
            menu:
                "Назвать число." if killedPeopleCount>0:
                    $ iscraPoints += 1
                    "— Всего-то [killedPeopleCount]."
                    isc "— А ты у нас убийца."
                    "— Да ты тоже. Мы же солдаты."
                    isc "— Давай дальше."
                "До миссии на корабле — ни одного.":
                    isc "— Разве? Ну ладно."
                    if killedPeopleCount==0:
                        $ liePoints += 1
                        "Правда и на корабле никого не убил. Но ведь я солдат. Должен убивать врагов."
                        # bulka: Так фактически же не солгал. Он не сказал же, что на корабле кого-то убил. Лишь не уточнил.
                    pi "— А на корабле сколько убил?"
                    isc "— Один ход — один вопрос! Давай дальше!"
                "Кажется, ты будешь первая.":
                    $ iscraPoints -= 1
                    $ agressionPoints += 1
                    "Искра обиженно отвернулась."
                    if yumingAlive == False or yumingPoints>2:
                        $ slavaPoints += 1
                        sl "— Ха, красава!"
                "С инферно? Или только обычных?":
                    $ tenkoAttention += 1
                    $ tenkoPoints += 1
                    "Я изобразил свой лучший оскал...{w} точнее улыбку."
                    if yumingAlive and yumingPoints<3 and minoriAlive:
                        $ minoriPoints -= 1
                        "Минори содрогнулась."
                        isc "— То есть ты убивал не только Инферно?"
                    else:
                        "Искру передёргнуло."
                        sl "— То есть ты убивал не только Инферно?"
                    "— А это уже второй вопрос."
                    "После этой улыбки уже помрачнели все."
                    
        "Действие":
            isc "— Сломай Славе нос, пожалуйста."
            pi "— Так, без насилия. Только доброта и лёгкие издевательства."
            isc "— Ладно. Тогда обними любого человека из присутствующих."
            call hugs
            # bulka: Появилась идея сделать Искру похожей на Милли Эшфорд из гиасса. Влюблена в гг, но помогает отношениям другой с ним. И что любовный рут с ней очень сложно достичь. Но перс твой, так что решать тебе.
            # pomis: Почему бы и нет. Вот только опять придётся много всего переписывать?
            # bulka: Кто знает. Я за её репликами не особо следил. Но вполне вероятно.
    "— Теперь моя очередь. Слава. Правда или действие?"
    sl "— Эм. Правда."
    # pomis: вот тут выбор просто так.
    menu:
        "— Кто из девушек больше всего нравится?":
            if yumingAlive:
                "Слава немного поробел, но вино сделало своё дело."
                sl "— Ну раз её тут нет, то скажу. Юминг. Она хоть и любит хвалить свой Китай, но мне нравится в ней почти всё."
                # bulka: И тут я понял, что тут нет Юминг...
                # bulka: Ну ничего - будет, куда денется.
                # pomis: А она тут нужна? Слава бы не смог так говорить в её присутствие
                # bulka: Ну чтобы у неё со Славой начали отношения развиваться. Меня в визуальных новеллах смущают две вещи: 1) когда гг портит отношения с девушкой, это никак не связывается на сюжете, разве что она не яндере. К примеру, тут можно грубить любому персонажу и кроме как на взаимодействии с тенко или соседом это никак не скажется почти. 2) Когда гг выбирает одну из девушек, то другие просто исчезают буквально. Максимум там счастья пожелают. Такое ощущение, что это гарем специально создавался под гг и это их единственное предназначение. А так это больше будет походить на реальность - если не пытаешься добиться девушки, то она может завести отношения с кем-то.
            else:
                sl "— Та, что пропала без вести..."
                "Мне не следовало задавать этот вопрос."
                # bulka: Мб тут пойнты или ещё чё сделать?
                # pomis: Какие?
                # bulka: В душе не клепу. А то тут как выбор между раком и спидом - никуя от него не меняется.
        "— Кто круче, Жорик или Иван?":
            sl "— Ха! Конечно Иван!"
            pi "— Дыа!"
        "— Что тебя больше всего бесит во мне?":
            if slavaPoints>3:
                sl "— Ты мне как брат, о чём ты?"
            else:
                sl "— Даже не знаю, что точно. Но что-то бесит. Немного."
                sl "— Какой-то ты недвижняковый."
                "Ну и словечко."
    if minoriAlive:
        # pomis: а тут хочу избежать обращения по имени. По спрайтам на экране будет понятно, к кому обращается.
        sl "— Итак. Правда или действие?"
        mi "— Эм-м... Правда. Мне нечего скрывать."
        sl "— Что ты хочешь больше всего?"
        if minoriPoints>=3:
            mi "— Быть полезной для тех, кто способен защитить меня."
        else:
            mi "— Ну... Я бы хотела вернуться домой, но это маловероятно."
            mi "— Наверное, довериться кому-то здесь."
        isc "— Не круто совсем. Давай дальше."
        mi "— Тенко? Правда или действие?"
    else:
        sl "— Итак. Тенко. Правда или действие?"
    ten "— Я бы вообще не хотел в этом участвовать, но, думаю, у меня нет выбора. Пусть будет правда."
    if antiTenkoPoints>2:
        "Не думаю, что он просто так расскажет правду."
    if minoriAlive:
        mi "— Что у тебя на уме?"
    else:
        sl "— Что у тебя на уме?"
    ten "— Ничего конкретного. Сейчас у меня нет забот."
    if antiTenkoPoints>1:
        "Как я и думал."
    ten "— Теперь я должен спросить Ивана?"
    sl "— Да к чёрту эту игру! Надоело!"
    "Иван облегчённо вздохнул."
    isc "— Как думаете, кто следующим умрёт из нас?"
    sl "— Точно не ты. Закон подлости никто не отменял."
    if minoriAlive==False:
        "— А тебе мало смертей?"
        isc "— Ты тоже вряд ли быстро умрёшь."
        isc "— Ставлю на нашего нового координатора!"
        pi "— Э-э... Почему я?"
        isc "— Предыдущий тоже недолго продержался."
        pi "— Знаешь, он мне был почти как друг."
        isc "— Почти как друг. Большая потеря?"
        sl "— Ну хватит уже..."
        isc "— Я была уверена, что Минори долго не продержится. И она умирает на первом же задании!"
        isc "— Так что, и сейчас я буду права. Скорее всего."
        pi "— Да ты надоела уже. Кто-нибудь, уведите её."
        sl "— Да с радостью.{w} Сама уйдёшь, или помочь?"
        if iscraPoints>minoriPoints:
            "Не нравится мне этот расклад..."
            menu:
                "Не вмешиваться":
                    isc "— Да что вы за люди такие, ладно, молчу..."
                    "Остаток вечера мы делились историями из своей жизни. Вернулись уже поздно ночью."
                    "Стоило мне коснуться постели, как я отключился."
                "— Может, тебе помочь успокоиться?":
                    $ stoodForIscra = True
                    $ leadershipPoints += 1
                    $ iscraPoints += 1
                    $ slavaPoints -= 1
                    sl "— А ты-то чего?"
                    "— Нам нельзя разделяться. Команда же всё-таки."
                    sl "— Она разрушает наш командный дух!"
                    "— А может, укрепляет?"
                    sl "— Каким ещё образом?"
                    "— Тем, что нужно понимать слабости, и стараться исправить их. Минори была слаба, это все понимали, но делали вид, что всё нормально."
                    call talkingWithIscraAndTenko

        else:
            isc "— Да что вы за люди такие, ладно, молчу..."
            "Остаток вечера мы делились историями из своей жизни. Вернулись уже поздно ночью."
            "Стоило мне коснуться постели, как я отключился."

    else:
        isc "— Ставлю на то, что умрёт Минори."
        # pomis: а вот сейчас диссонанс для япошки с бзиком на имена\фамилии.
        if minoriPoints>=3 and minoriPoints>iscraPoints:
            "А вот это уже выходит за все рамки." with bloodrage
            "— А может, всё таки, ты? Прямо здесь?"
            $ minoriPoints += 1
            # pomis: хоть здесь и не зависит напрямую от выбора, пусть будет так.
            jump fight
        else:
            menu:
                "— Умереть может кто угодно. Какой смысл гадать?":
                    isc "— Интересно же. Думаю, ты бы тоже хотел знать."
                    "— Знать, но не гадать же."
                    isc "— Ладно, ладно. Но всё-таки, Минори умрёт первая."
                    sl "— Да закройся ты!"
                    menu:
                        "Промолчать":
                            isc "— Кому пора закрыться, так это тебе. Всё время какие-то глупости говоришь."
                            pi "— Ну хватит уже. Не портите вечер."
                            "Каким-то чудом Искра замолчала."
                            # pomis: просто мне пока лень ещё одну ссору расписывать
                            "Остаток вечера мы делились историями из своей жизни. Вернулись уже поздно ночью."
                            "Стоило мне коснуться постели, как я отключился."
                        "— А может, она права?":
                            $ stoodForIscra = True
                            $ leadershipPoints += 1
                            $ iscraPoints += 1
                            $ minoriPoints -= 2
                            $ slavaPoints -= 2
                            "— Минори не способна постоять за себя. Она может стать лёгкой добычей для врага, да и подвести нас в любой момент."
                            "Слава и Минори были шокированы моей честностью, Тенко загадочно улыбнулся." 
                            isc "— Ну хоть кто-то не стесняется своего мнения. Вы все так думаете. Но строите из себя чёрт знает, кого."
                            call talkingWithIscraAndTenko

                "— А может, всё таки, ты? Прямо здесь?":
                    $ agressionPoints += 1
                    $ minoriPoints += 1
                    jump fight
    if inferno:
        jump ch5enlil
    else:
        jump ch5day0


label hugs:
    menu:
        "Иван" if underVine:
            $ slavaPoints -= 1
            "— Я не хочу, чтобы ты умер так же как Жорик!"
            "С этими словами я приобнял нашего координатора. Я чуть не расплакался. Трезвым бы я такого не сделал."
            pi "— Я бы лучше кого-то из девушек обнял, странный выбор."
            return
        "Искра":
            if minoriPoints>4 and minoriAlive:
                $ minoriPoints -= 1
            $ iscraPoints += 1
            "Не успела она договорить, как я прижал её к себе и слегка погладил по спине."
            "Искра залилась румянцем и замолчала."
            return
        "Слава" if underVine:
            $ slavaPoints += 1
            "— Братан, я выбираю тебя!"
            sl "— Иди ко мне, брат!"
            isc "— Кто бы мог подумать..."
            return
        "Минори" if minoriAlive:
            if minoriPoints>3:
                "Я обнял тихую японку. Она шепнула мне на ухо, что я всё сделал правильно."
                $ minoriPoints += 1
                return
            else:
                "Я обнял тихую японку. Кажется, ей было абсолютно всё равно."
                mi "— Неожиданно..."
                return
        "Тенко" if underVine:
            "Ну уж нет. Я же немного выпил, почему мне в голову лезут такие мысли. Надо с девушками."
            "Я даже протрезвел."
            $ underVine = False
            call hugs

label fight:
    ten "— Интересное развитие событий..."
    isc "— Посмотрим, на что ты годен?"
    $ iscraPoints -= 3
    "Искра подбежала ко мне и резко влепила пощечину."
    # bulka: см камент 2.
    if slavaPoints>2:
        sl "— Ты как посмела тронуть моего брата? Немедленно извинись перед ним!"
    else:
        sl "— Ты как посмела тронуть моего соседа?"
    "Минори подбежала ко мне, нежно провела рукой по щеке."
    mi "— Ты в порядке?"
    "— Да всё норм..."
    "Пока я приходил в себя уже успели переругаться."
    isc "— А ты что сделаешь? Тоже убьёшь?"
    sl "— Если придётся."
    isc "— Да ты слабак. Сколько людей ты убил до корабля? Одного? Вряд ли даже одного."
    "— Это ты слабачка. Только и можешь, что ссорить людей."
    isc "— Я слышу писк какой-то..."
    pi "— Да прекратите вы уже наконец!"
    if withMinoriOnZumwalt:
        isc "— Минори, не надоело всегда прятаться за чужую спину?"
        isc "— Бесполезное создание."
        "— Да замолчи ты уже!" with bloodrage
        if killedPeopleCount>0:
            "Я уже плохо контролирую себя. Как бы не сорваться и не пополнить список трупов."
    else:
        isc "— Да, утихомирь этих псов!"
    isc "— Ненавижу вас."
    ten "— Сама всех настроила против себя же."
    isc "— Я что, виновата, что вы такие ранимые?"
    "Тон Искры сменился с агрессивного на отчаянный."
    # bulka: Перечитывая момент показалось странным, что она не думает о том, что говорит, да ещё признаёт это.
    # pomis: Так?
    "Вечер однозначно был испорчен, мы пошли домой."
    if minoriPoints>=3:
        "Я шёл с Минори и болтал о всякой ерунде. Слава с Иваном обсуждали достижения техники, Тенко надел наушники и молча шёл за нами, а Искра плелась сзади."
    else:
        "Я шёл и обсуждал с Иваном достижения техники, Слава о чём-то болтал с Минори. Тенко надел наушники и молча шёл за нами, а Искра плелась сзади."
    "Наконец я добрался до нашей служебной квартиры и отключился." with closing
    if inferno:
        jump ch5enlil
    else:
        jump ch5day0

label talkingWithIscraAndTenko:
    # bulka: Решил издевнуться над файлом и вместо вложенности сделать, чтобы по всему файлу прыгать приходилось?)
    ten "— В целом, я согласен."
    sl "— Вот значит как."
    "Остаток вечера мы провели в разрозненных компаниях. Хоть Тенко всегда казался мне самым пугающим персонажем среди новобранцев, я смог пообщаться с ним."
    ten "— [playerName], а ты знаешь появились Инферно? Я так и не нашёл никакой информации."
    if visitedYumingRoom:
        $ iscraPoints += 1
        # bulka: Прикольно что, чтобы получить очко от ревнивицы, надо зайти в комнату другой девушкиXD
        "— Мне самому интересно. Ты слышал про некого Айсдена? Говорят, это самый крутой шпион, который следит за Инферно."
        isc "— Рихард Айсден? Я вся внимание!"
        ten "— Впервые слышу. И о чём он говорит?"
        "— Ну, например, у них там довольно строгая иерархия. А тот, кто принимает неверные решения, быстро теряет свою власть. Вроде так. Но про происхождение Инферно ни слова."
        ten "— Откуда у него такие данные?"
        "— Я так понял, что он перехватывает коммуникации по всему миру и как-то анализирует всё это. У него в компании, должно быть, лучшие умы преступного мира. Но всё же, этой информации на все сто доверять не стоит."
        ten "— Почему нам не рассказывают об этом наши координаторы?"
        "— Они, может, даже не в курсе."
        isc "— Я тоже читала один его доклад. Есть фотка на телефоне. Почитаете?"
        ten "— Ты ещё спрашиваешь."
        isc "— Судя по всему, мой доклад был раньше твоего. Ладно, сейчас открою фотку."
    else:
        "— Понятия не имею."
        isc "— Я немного рылась на базе... Искала информацию."
        isc "— Нашла доклад некого Рихарда Айсдена. Могу поделиться с вами, там довольно интересная информация."
        # pomis: если игрок читает это, то он 50% попадает в рут Энлиль. И портальщик имеет косвенное отношение к изложенному.
        # bulka: 50%? Шта?
        # pomis: вероятность. 
    nvlc "*ДОКЛАД Р. АЙСДЕНА ЗА ЯНВАРЬ 2049. ИНЦИДЕНТ В ТОКИО.*"
    nvlc "Вы уже слышали о загадочных событиях в Токио? Власти всячески пресекают распространение любой информации. А там в течение месяцев происходила какая-то чертовщина: люди теряли память, или наоборот, обретали память другого человека. Множество внезапных смертей, которые не могут объяснить врачи. Многочисленные исчезновения. Кто-то пускает информацию, что это вирус, действующий на мозг, но мои источники утверждают, что всё гораздо хуже. Во-первых, были зафиксированы волны неизвестной нам природы, возникавшие перед инцидентами. А также, множество перехваченных разговоров на неизвестном языке. Наши лингвисты работают над расшифровкой, но сейчас мы можем с уверенностью заявить, что Инферно пытались разрушить психическое здоровье жителей мегаполиса. Возможно, это была проба технологии, и, вскоре, они испытают это на других городах. Следующий доклад ровно через месяц."
    $ icedenReportsCount += 1
    # pomis: на самом деле, это никакая не попытка разрушить психическое здоровье. 
    # bulka: косяк в использовании?
    # pomis: опыты на людях
    "— Интересно бы найти следующий доклад."
    sl "— Всё не могу понять, почему они не в открытом доступе?"
    isc "— Солдатам не нужно знать слишком много."
    if visitedYumingRoom:
        "— Да и информация не самая проверенная."
    "— Может, спросить кого-то? Возможно, завалялись остальные доклады?"
    ten "— Не думаю, что вообще предполагается, что мы можем об этом знать. Лучше не рисковать в открытую."
    if visitedYumingRoom:
        "— Я спрошу у своего источника."
        ten "— Держи в курсе."
    if tenkoKilledNavy:
        isc "— Кстати, Тенко, как ты стал таким хладнокровным убийцей? Я до сих пор вспоминаю тот случай на корабле..."
        ten "— Я был уверен, что они не люди..."
    if tenkoSaidAboutReport:
        "— Кстати, Тенко, что за отчёт ты писал о нашей операции?"
        ten "— Отчёт?{w} А, Жорик поручил мне составлять отчёты об операциях. Спихнул свою обязанность, а потом умер."

    "— Кстати, а кто что знает о Павле?"
    ten "— Шпионил за нами, я должен был это заметить."
    isc "— Павел шпионил? Я что-то пропустила?"
    "— Да, утреннее собрание."
    isc "— Меня туда никто и не позвал.{w} Не важно. И кто такой Павел?"
    ten "— Наш бывший товарищ."
    if inferno:
        "— Было бы интересно выйти с ним на связь."
    else:
        "— Было бы интересно понять, почему он против нас."
    pi "— Эй, хватит уже шептаться с таким серьёзным видом! Пошли уже по домам!"
    "Мы направились домой теми же компаниями. По пути мы с Искрой строили всевозможные теории, а Тенко критиковал их."
    "Мы уже дошли до нашего временного дома."
    if iscraPoints>minoriPoints and iscraPoints>yumingPoints and iscraPoints>mariePoints:
        call iscraRoom
    # bulka: Не понимаю — почему если минори мертва, гг не может быть с искрой? Типо слишком большая травма от потери минори? Но любовь-морковь начинается с 3 пойнтов, а минори может быть мертва и при 2.
    # pomis: Я просто не смог ещё придумать диалог без Минори
    # bulka: То есть в остальном будет примерно также?
    # pomis: Да.
    else:
        "Наконец я добрался до служебной квартиры и отключился, едва коснувшись постели." with closing
        # bulka: И этот человек мне что-то про вложенность рассказывал...
        # pomis: Верно подмечено. Вывел в отдельный фрагмент. Если гг проебал момент туда попасть, то, может, в другой раз получится как-нибудь.
    return


label iscraRoom:
    isc "— Так не хочется спать... [playerName], что будешь делать?"
    "— Ты не поверишь..."
    isc "— Ну ладно."
    "— А что, есть варианты?"
    "Искра промолчала в ответ."
    # сцена подъезд
    "Интересно, она ведь пыталась на что-то намекнуть?"
    "Из всех девушек, она мне нравится больше всего. Хоть она и параноик немного... Но излишняя осторожность в нашей ситуации не помешает."
    "Я последним вошёл в подъезд. На втором этаже Тенко и Искра разошлись по своим квартирам. Искра открывала дверь нарочито медленно и пару раз взглянула на меня, пока я поднимался на её этаж."
    "Могла бы просто пригласить."
    "— А с кем ты живёшь?"
    isc "— Одна. Никто со мной ужиться не может... Была соседка, но убежала в панике. "
    "— Что же ты с ней сделала?"
    isc "— Просто поддерживала разговор..."
    # bulka: Не уверен, но имхо тут небольшое несочетание совершенного и несовершенного глаголов.
    "— Зайду к тебе? Совсем не хочется торчать в компании обидевшегося на меня Славы."
    isc "— Да, заходи!"
    scene bg city room night
    show iscra sad at right

    "Мы расположились у неё на кухне. Такая же ужасная кухня, как и у меня со Славой."
    isc "— Будешь виноград? Я сегодня весь город обошла, чтобы найти хоть немного зелёного винограда."
    "— Не откажусь."
    menu:
        "— Слушай, а почему ты постоянно всех провоцируешь?":
            isc "— Ну...{w} Думаю, я просто не умею по-другому общаться с людьми."
            isc "— Скорее, я не умею по-другому вливаться в коллектив."

            menu:
                "— Ты так только отталкиваешь их.":
                    $ iscraPoints += 2
                    show iscra smiling at right

                    isc "— Не всех ведь? А мне и не нужна всенародная любовь."
                    "Сама вывела разговор в это русло. Теперь отбивайся."
                    "— А чья любовь тебе нужна?"
                    "Искра слегка смутилась."
                    isc "— Достойных людей."
                    "— Что отличает достойных людей от недостойных?"
                    isc "— Думаю, наличие разума."
                    "— Слава разумен?"
                    isc "— Конечно, нет!"
                    "— А я?"
                    isc "— Ты вроде бы разумнее."
                    "— То есть, достойный человек?"
                    isc "— Ну да. Чего заладил-то?"
                    "— Выходит, тебе нужна моя любовь? А что я получу в ответ?"
                    "Застал её врасплох таким вопросом."
                    isc "— Думаю, ты и так всё понял. И уже давно."
                "— Зачем тебе вообще пытаться влиться в коллектив?":
                    isc "— Чтобы не умереть со скуки. Да и просто не умереть. Всё-таки, мы же солдаты!"
                    "— Солдаты, да. Смогла бы доверить свою жизнь таким товарищам?"
                    isc "— Один раз уже пришлось, живая осталась."
                    "— Чем ты вообще занималась в той операции? Я тебя заметил только в конце."
                    isc "— Эм... Пряталась за вашими спинами. Прикрывала сзади."
                    "— А ты опасная!"
                    if withMinoriOnZumwalt:
                        isc "— А Минори я возненавидела когда она навязалась тебе при зачистке корабля. Мне тоже было жутко страшно... Но до такого я не докатилась."
                        "— Но ты же пряталась за спинами."
                        isc "— Прикрывала сзади же! Я хотя бы не была обузой."
                        # pomis: я даже не представляю какой процент игроков сможет увидеть эти реплики. 0.1% наверное.
                        # bulka: Неужели так сложно?
                        # pomis: довольно-таки да
        "— Почему именно виноград?":
            $ iscraPoints += 1
            isc "— Он лучший."
            "— Убедительно. У меня сестра тоже очень любила виноград..."
            isc "— А сейчас?"
            "— А сейчас её нет..."
            isc "— Почему?{w} Я не умею играть в сочувствие, извини..."
            isc "— Да и у меня даже двоюродной сестры или брата нет. Мне не понять, что ты чувствуешь."
            if inferno: 
                "— Военные потопили наш корабль. Я выжил, а моя семья — нет."
                isc "— Какие ещё военные?"
                "— Я не знаю, кто именно."
                isc "— Из этой нашей организации? Ты по этому вступил в неё? Найти и уничтожить?"
                "— Хотя бы выяснить правду."
                isc "— А потом наказать всех виновных! Мне нравится."
            else:
                "— Наш корабль был потоплен инферно. Мне повезло, а вот моей семье — нет."
                isc "— Ты по этому вступил в армию? Найти и уничтожить виновных?"
                # bulka: Виновен я, не виноват! Виновны мы вместе – мы заложники мести.
                # pomis: Чойта значит?
                # bulka: Мюзикл Ромео и Джульетта, второй акт, ария пятая. После убийства Тибальда за то, что тот убил Меркуцио, Ромео признаётся и поёт эти строки.
                "— Хотя бы выяснить правду."
                isc "— А потом наказать всех виновных! Мне нравится."
            isc "— Если что, я с тобой."
            "— Знаешь, это гораздо лучше, чем если бы ты притворилась, что тебе жаль мою семью."
    nvl clear        
    hide iscra
    nvlc "Ещё час мы разговаривали обо всём подряд."# TODO: Какие-нибудь подробности.
    nvlc "Мне настолько не хотелось возвращаться к Славе, что я лёг спать прямо тут, притворившись, что непроизвольно отключился."
           # дальше — больше
            # bulka: Изи катка чёт
            # pomis: хотел сделать фразы на выбор, но не смог их придумать
    $ sleptInIscraRoom = True
    return
    
    # bulka: Чёт догоняем по размеру рекордный ch3day2
    # pomis: И хорошо, и плохо
    # pomis: Чёт уже в пиздец превращается. Но тут хотя бы почти линейно.